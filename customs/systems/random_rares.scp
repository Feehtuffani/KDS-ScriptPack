///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Random Rares 1.2 - Scripted by Mordaunt
// 
// No Gembits so no issue with sector sleep.
// Will generate a random NPC in a random location on the map every <x> days
// Will generate a random locked Loot Chest in a random location on the map every <x> days (different timer)
// Will not spawn on water 
// Chests will decay between 3 and <chest_spawn> after spawning.
// Log details left in place so admin can check on functionality of script 
//
// DO NOT SET CHEST SPAWN TO LESS THAN 4 DAYS WITHOUT CHANGING THE MINIMUM TIMER IN THE F_SET FUNCTION
// Doing so may cause unwanted results.
//
// CHECK/CUSTOMISE DORAND LOOPS PRIOR TO USE. NPC's HAVE BEEN UPDATED TO NEW SCRIPT PACK!
//
// Installation:
//		Add your rare NPCs inside the DORAND loop of [FUNCTION f_rare_npcspawn]
//		and ensure the number after DORAND is equal to the number of lines before the ENDDO
//		(Mustangs were added purely for example/testing purposes)
//
//		Change chest loot/unlock difficulty to suit your tastes. (more2 on each chest) or put your own chests into the system.
//
//		Under f_onserver_timer in sphere_serv_triggers.scp
//		add: rare_npc_timer and loot_chest_timer
//
// 		In sphere.ini make sure there is a value other than 0 here:
//		TimerCall=
//		Doesn't matter what it is if you are already using it for other functions.
//
//		If there are any issues with this script spawning an npc/chest on water/rock:
//		1. Check the type of the tile it has spawned on, 
//		2. If it shows type t_normal, open sphere_types.scp and add the ID to the TYPEDEF t_water (or t_rock) block. 
//			(server will require a restart to reflect the change)
//
//		Note: NPCs are generated without gembits and are not removed on any timer and so will remain in the wild until they 
//		      are killed/tamed or die. 
//
//	You're done!
//
// New 1.1: 	ADDED: 	Chests/NPC's per pass (Requested by Dagger4k)
//		FIXED: 	Incorrect more2 settings on chests
//
// New 1.2:	ADDED: 	Timer to remove "untamed" rares NPC between 3 and <npc_spawn>+1 days after spawning with def to enable this new option.
//		 		- This was originally omitted on purpose, but then Rattlehead got me doubting that decision. Damn you Rattlehead! lol
//		ADJUSTED: Decay timer for chests to allow upper limit of <chest_spawn>+1 to allow a little overlap, reducing chance of zero chests remaining in the world.		     		  
//		
//
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////		

[DEFNAME rare_timer]
npc_spawn 7  		//time between spawn in days
npc_count 2		// # of NPCs to spawn each pass
chest_spawn 5 		//time between spawn in days
Chest_Count 5		// # of chests to spawn each pass
moveable_chest 1 	//can player pick up the chest?
npc_despawn 1		//rare NPCs will be despawned prior to new ones being spawned (Mounts will not despawn 


[FUNCTION rare_npc_timer]
local.push=npc
IF (<eval ((<serv.time>/864000) - <var0.last_rare_npc>)> > <ddef.npc_spawn>)  
	for 0 <eval <def.npc_count>-1>
		randomxy <local.push>
	endfor
	var.last_rare_npc = <eval (<serv.time>/864000)>
else
	return 1
endif


[FUNCTION loot_chest_timer]
local.push=chest
IF (<eval ((<serv.time>/864000) - <var0.last_loot_chest>)> > <ddef.chest_spawn>) 
	for 0 <eval <def.chest_count>-1>
		randomxy <local.push>
	endfor
	var.last_loot_chest = <eval (<serv.time>/864000)>
else
	return 1
endif


[Function randomxy]
local.push=<args>
local.x=<eval RAND(4906)>
local.y=<eval RAND(4096)>
call f_spawn_loc

[Function f_spawn_loc]
if (<SERV.MAP(<dlocal.x>,<dlocal.y>).region.FLAGS> & region_flag_guarded) || (<SERV.MAP(<dlocal.x>,<dlocal.y>).region.FLAGS> & region_flag_underground)
	randomxy <local.push>
	return 1
else
	if (<SERV.MAP(<dlocal.x>,<dlocal.y>).TYPE>==t_water) || (<SERV.MAP(<dlocal.x>,<dlocal.y>).TYPE>==t_rock)
		randomxy <local.push>
		return 1
	else
		if (STRMATCH(*npc*, <local.push>))
			call f_rare_npcspawn
		else
			call f_loot_chest_spawn
		endif
	endif
endif

[Function f_rare_npcspawn] //special mounts or special mobs to kill are all good in here.
DORAND 10   //add your NPCs after this and change the number to match how many lines are inside the DORAND
	serv.newnpc c_horse_mustang
	serv.newnpc c_llama_ethereal
	serv.newnpc c_steed_ethereal
	serv.newnpc c_steed_fire
	serv.newnpc c_unicorn
	serv.newnpc c_ostard_zostrich
	serv.newnpc c_wyrm_ancient
	serv.newnpc c_treefellow
	serv.newnpc c_werewolf
	serv.newnpc c_beetle_fire
ENDDO
NEW.P <dlocal.x>, <dlocal.y>
NEW.FIX
if <def.npc_despawn>
	local.timer=<eval ((<ddef.npc_spawn>+1)*86400)>
	NEW.TIMERF <R259200,<dlocal.timer>>, f_despawn
endif
serv.log Rare <new.name> Spawned @ <dlocal.x>,<dlocal.y>

[Function f_loot_chest_spawn]
// These chests are further down the script and are filled from standard templates.
// You can add your own chests here (see instructions for f_set), or edit the ones at the bottom of the script.
DORAND 5 
	serv.newitem i_loot_chest_poor
	serv.newitem i_loot_chest_meagre
	serv.newitem i_loot_chest_average
	serv.newitem i_loot_chest_rich
	serv.newitem i_loot_chest_filthy_rich
ENDDO
NEW.P <dlocal.x>, <dlocal.y>
NEW.FIX
serv.log Rare <new.name> Spawned @ Spawned @ <dlocal.x>,<dlocal.y>

[function f_set]  
//Set chest as locked (and moveable if enabled) and set decay timer.  
//if you add any chests make sure to add this event to them in the manner used on the chests below.
local.timer= <eval ((<ddef.chest_spawn>+1)*86400)>
type=t_container_locked
if (<def.moveable_chest>==1)
	attr=02
else
	attr=012	
endif
timer=<R259200,<dlocal.timer>> //Chest will decay at a random time from 3 days (in seconds) to respawn time +1

[function f_despawn]
if (<flags>&statf_pet)
	return 1
else
	remove
endif 

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Begin Loot Chests - Loot levels are not set high by default - Adjust as needed
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

[ITEMDEF i_loot_chest_poor]
ID=0e43
TYPE=T_CONTAINER
TDATA2=049
FLIP=1
WEIGHT=10
CATEGORY=PROVISIONS - MISCELLANEOUS
SUBSECTION=LOOT CONTAINERS
DESCRIPTION=POOR LOOT CHEST

ON=@Create
	timerf 1, f_set
	more2={200 300}  // <--- lockpick difficulty
	ref1=<uid>
	SERV.NEWITEM={ goodie_poor_1 1 goodie_poor_2 1 goodie_poor_3 1 goodie_poor_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_poor_1 1 goodie_poor_2 1 goodie_poor_3 1 goodie_poor_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_poor_1 1 goodie_poor_2 1 goodie_poor_3 1 goodie_poor_4 1 }
	NEW.CONT=<ref1>

[ITEMDEF i_loot_chest_meagre]
ID=0e43
TYPE=T_CONTAINER
TDATA2=049
FLIP=1
WEIGHT=10
CATEGORY=PROVISIONS - MISCELLANEOUS
SUBSECTION=LOOT CONTAINERS
DESCRIPTION=MEAGRE LOOT CHEST 

ON=@Create
	timerf 1, f_set
	more2={400 600}
	ref1=<uid>
	SERV.NEWITEM={ goodie_meager_1 1 goodie_meager_2 1 goodie_meager_3 goodie_meager_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_meager_1 1 goodie_meager_2 1 goodie_meager_3 goodie_meager_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_poor_1 1 goodie_poor_2 1 goodie_poor_3 1 goodie_poor_4 1 0 4}
	NEW.CONT=<ref1>

[ITEMDEF i_loot_chest_average]
ID=09AB
TYPE=T_CONTAINER
TDATA2=049
FLIP=1
WEIGHT=10
CATEGORY=PROVISIONS - MISCELLANEOUS
SUBSECTION=LOOT CONTAINERS
DESCRIPTION=AVERAGE LOOT CHEST 

ON=@Create
	timerf 1, f_set
	more2={700 850}
	ref1=<uid>
	SERV.NEWITEM={ goodie_average_1 1 goodie_average_2 1 goodie_average_3 1 goodie_average_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_average_1 1 goodie_average_2 1 goodie_average_3 1 goodie_average_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_meager_1 1 goodie_meager_2 1 goodie_meager_3 goodie_meager_4 1 }
	NEW.CONT=<ref1>

[ITEMDEF i_loot_chest_rich]
ID=09AB
TYPE=T_CONTAINER
TDATA2=04A
FLIP=1
WEIGHT=20
CATEGORY=PROVISIONS - MISCELLANEOUS
SUBSECTION=LOOT CONTAINERS
DESCRIPTION=RICH LOOT CHEST 

ON=@Create
	timerf 1, f_set
	more2={950 990}
	ref1=<uid>
	SERV.NEWITEM={ goodie_rich_1 1 goodie_rich_2 1 goodie_rich_3 1 goodie_rich_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_rich_1 1 goodie_rich_2 1 goodie_rich_3 1 goodie_rich_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_average_1 1 goodie_average_2 1 goodie_average_3 1 goodie_average_4 1 }
	NEW.CONT=<ref1>

[ITEMDEF i_loot_chest_filthy_rich]
ID=0e41
TYPE=T_CONTAINER
TDATA2=042
FLIP=1
WEIGHT=20
CATEGORY=PROVISIONS - MISCELLANEOUS
SUBSECTION=LOOT CONTAINERS
DESCRIPTION=FILTHY RICH CHEST 

ON=@Create
	timerf 1, f_set
	more2=1000
	ref1=<uid>
	SERV.NEWITEM={ goodie_loaded_1 1 goodie_loaded_2 1 goodie_loaded_3 1 goodie_loaded_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_loaded_1 1 goodie_loaded_2 1 goodie_loaded_3 1 goodie_loaded_4 1 }
	NEW.CONT=<ref1>
	SERV.NEWITEM={ goodie_rich_1 1 goodie_rich_2 1 goodie_rich_3 1 goodie_rich_4 1 }
	NEW.CONT=<ref1>	



[eof]